
///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////

scoreboardteamcard = [
	arg3 = (|A 0xB0 $arg3)
	arg4 = (|A 0xB0 $arg4)
	arg6 = (= $arg1 2)    // quick-check for opposite alignment
	uigroup [
		local bit cc cell0 cell1 cell2 cell3 cell4 cell5 lbar rbar
		if (>= $arg1 0) [
			if (m_ctf $getmode) [ bit = (^ $bit 0x01) ; ++ cc ]
			if $showkills       [ bit = (^ $bit 0x02) ; ++ cc ]
			if $showdeaths      [ bit = (^ $bit 0x04) ; ++ cc ]
			if $scoreboardmultiplayer [
				if $showpj      [ bit = (^ $bit 0x08) ; ++ cc ]
				if $showping    [ bit = (^ $bit 0x10) ; ++ cc ]
			]
		] ; if $showclientnum   [ bit = (^ $bit 0x20) ; ++ cc ]

		//    name cell size   +   cell size * cc   +   cell spacing * cc
		x = (+f $.UI_pad_UXS (*f $.UI_pad_6XL $cc) (*f $.UI_pad_3XL $cc))

		lbar = [ ? (&& (! @arg6) [& $bit $arg1]) [ UIbar 0 1 0 0 @@arg4 ] ]
		rbar = [ ? (&&    @arg6  [& $bit $arg1]) [ UIbar 0 1 0 0 @@arg4 ] ]

		cell0 = (? (& $bit 0x1) [
			uifill $.UI_pad_6XL 0 [ uitext (getclientscore $cn) 0.6 ]
		])
		cell1 = (? (& $bit 0x2) [
			@(lbar 0x1)
			uifill $.UI_pad_6XL 0 [ uitext (getclientfrags $cn) 0.6 ]
			@(rbar 0x1)
		])
		cell2 = (? (& $bit 0x4) [
			@(lbar 0x3)
			uifill $.UI_pad_6XL 0 [ uitext (getclientdeaths $cn) 0.6 ]
			@(rbar 0x3)
		])
		cell3 = (? (& $bit 0x8) [
			@(lbar 0x7)
			uifill $.UI_pad_6XL 0 [
				if (scoreboardpj $n) [
					uitext (scoreboardpj $cn) 0.6
				] [ uifonttext "wide" "^f4-" 0.6 ]
			]
			@(rbar 0x7)
		])
		cell4 = (? (& $bit 0x10) [
			@(lbar 0xF)
			uifill $.UI_pad_6XL 0 [
				if (scoreboardping $n) [
					uitext (scoreboardping $cn) 0.6
				] [ uifonttext "wide" "^f4-" 0.6 ]
			]
			@(rbar 0xF)
		])
		cell5 = (? (& $bit 0x20) [
			@(lbar 0x1F)
			uifill $.UI_pad_6XL 0 [ uitext $cn 0.6 ]
			@(rbar 0x1F)
		])

		uialign 0 -1
		uigrid $arg2 $arg5 0 [
			if (>= $arg1 0) [ uifill $x ; uifill $x ]
			loopscoreboard cn $arg1 [
				uicolor $arg3 $x $.UI_pad_5XL [
					style_generic_gradient2
					UIfastimgtile "ui/" "ui_bg" $arg6
					uioutline $arg4
					uispace $.UI_pad_M 0 [
						if $arg6 [
							uihlist $.UI_pad_L [ loop+* n 5 -1 6 [ do [cell@n] ] ] ; uialign- -1
							uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6 ; uialign- 1
						] [
							uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6 ; uialign- -1
							uihlist $.UI_pad_L [ loop n 6 [ do [cell@n] ] ] ; uialign- 1
						]
						uiclamp*y
					]
					uiclamp*e
				]
			]
		]
	]
]

UIquickdialog "scoreboard" [
	uiallowinput 0
	refreshscoreboard
	uiclamp.e
] [
	// keeping these around just to remember them >_>
	// uitext $scoreboardservinfo 0.85 // server's name
	// uicolortext (getteamscore 1) $_c_blue2 1.5
	// uicolortext (getteamname 2) 0x38FFFFFF 1.6
	uivlist 0 [
		uialign 0 -1
		if (m_timed $getmode) [
			uihlist $.UI_pad_MS [
				local hue ; hue = (? (|| $intermission $paused) $c_orange $c_white)
				// var scoreboardtime otherwise
				uifontcolortext "mono_outline"       (div $timeremaining 60)    $hue 1.2
				uifontcolortext "wide_outline" ":"                              $hue 1.2
				uifontcolortext "mono_outline" (pad0 (mod $timeremaining 60) 2) $hue 1.2
			]
		] [ uitext " " 1.2 ]

		uifill 0 $.UI_pad_5XL
		uivlist $.UI_pad_3XL [
			if (m_teammode $getmode) [
				uigroup [
					uifont "wide_outline" [
						uioffset (!f $.UI_pad_2XL) (!f $.UI_pad_2XL) [ uitext "V" 1 ]
						uioffset     $.UI_pad_2XL      $.UI_pad_2XL  [ uitext "S" 1 ]
					]
					uihlist $.UI_pad_DXL [
						scoreboardteamcard  1 1 0x203070 0x284890
						scoreboardteamcard  2 1 0x702020 0x882828
					]
				]
				scoreboardteamcard -1 3 0x303840 0x404860 $.UI_pad_L
			] [
				uigroup [
					uifont "wide_outline" [
						uioffset (!f $.UI_pad_2XL) (!f $.UI_pad_2XL) [ uitext " " 1 ]
						uioffset     $.UI_pad_2XL      $.UI_pad_2XL  [ uitext " " 1 ]
					]
					uigroup [ scoreboardteamcard  0 2 0x303030 0x404040 $.UI_pad_DS ]
				]
				scoreboardteamcard -1 3 0x303840 0x404860 $.UI_pad_L
			]
		]
	]
	uigroup [
		uialign 0 1
		uifonttext "wide_outline" (? $scoreboardmap $scoreboardmap) 1
	]
] [ UIsetbgblur 1 ] [ UIsetbgblur -1 ]
