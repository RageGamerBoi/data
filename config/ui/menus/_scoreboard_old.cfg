
///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////

scoreboardteamcard = [
	arg3 = (|A 0xB0 $arg3)
	arg4 = (|A 0xB0 $arg4)
	uigroup [
		local bit cc
		if (>= $arg1 0) [
			if (m_ctf $getmode) [ bit = (^ $bit  1) ; ++ cc ]
			if $showkills       [ bit = (^ $bit  2) ; ++ cc ]
			if $showdeaths      [ bit = (^ $bit  4) ; ++ cc ]
			if $scoreboardmultiplayer [
				if $showpj      [ bit = (^ $bit  8) ; ++ cc ]
				if $showping    [ bit = (^ $bit 16) ; ++ cc ]
			]
		] ; if $showclientnum   [ bit = (^ $bit 32) ; ++ cc ]
		x = (+f $.UI_pad_UXS (*f $.UI_pad_6XL $cc) (*f $.UI_pad_3XL $cc))

		uialign -2 -1
		uifill $x
		uigrid $arg2 $arg5 0 [
			loopscoreboard cn $arg1 [
				uicolor $arg3 $x $.UI_pad_5XL [
					style_generic_gradient2
					case $arg1 1 [
						uitiledimage "data/interface/ui/ui_bg0.png" $.UI_pad_SXL $.UI_pad_SXL
					] 2 [
						uitiledimage "data/interface/ui/ui_bg1.png" $.UI_pad_SXL $.UI_pad_SXL
					]
					uioutline $arg4
					uispace $.UI_pad_M 0 [
						uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
						uialign- -1
						uihlist $.UI_pad_L [
							uiclamp.y
							uialign 1
							if (& $bit 1) [
								uifill $.UI_pad_6XL 0 [
									uitext (getclientscore $cn) 0.6
								]
							]
							if (& $bit 2) [
								if (^ $bit 2) [ UIbar 0 1 0 0 $arg4 ]
								uifill $.UI_pad_6XL 0 [
									uitext (getclientfrags $cn) 0.6
								]
							]
							if (& $bit 4) [
								if (^ $bit 4) [ UIbar 0 1 0 0 $arg4 ]
								uifill $.UI_pad_6XL 0 [
									uitext (getclientdeaths $cn) 0.6
								]
							]
							if (& $bit 8) [
								if (^ $bit 8) [ UIbar 0 1 0 0 $arg4 ]
								uifill $.UI_pad_6XL 0 [
									if (scoreboardpj $n) [
										uitext (scoreboardpj $cn) 0.6
									] [ uifont "wide" [ uitext "^f4-" 0.6 ] ]
								]
							]
							if (& $bit 16) [
								if (^ $bit 16) [ UIbar 0 1 0 0 $arg4 ]
								uifill $.UI_pad_6XL 0 [
									if (scoreboardping $n) [
										uitext (scoreboardping $cn) 0.6
									] [ uifont "wide" [ uitext "^f4-" 0.6 ] ]
								]
							]
							if (& $bit 32) [
								UIbar 0 1 0 0 $arg4 
								uifill $.UI_pad_6XL 0 [
									uitext $cn 0.6
									uialign- 1
								]
							]
						]
					]
					uiclamp*e
				]
			]
		]
	]
]

UIquickdialog "scoreboard" [
	uiallowinput 0
	refreshscoreboard
	uiclamp.e
] [
	// keeping these around just to remember them >_>
	// uitext $scoreboardservinfo 0.85 // server's name
	// uicolortext (getteamscore 1) $_c_azul2 1.5
	// uicolortext (getteamname 2) 0x38FFFFFF 1.6
	uivlist 0 [
		uialign 0 -1
		if (m_timed $getmode) [
			uifont "mono_outline" [
				uihlist $.UI_pad_MS [
					local hue ; hue = (? (|| $intermission $paused) $c_orange $c_white)
					// var scoreboardtime otherwise
					uicolortext       (div $timeremaining 60)    $hue 1.2
					uifont "wide_outline" [ uicolortext ":"      $hue 1.2 ]
					uicolortext (pad0 (mod $timeremaining 60) 2) $hue 1.2
				]
			]
		] [ uitext " " 1.2 ]
		
		uifill 0 $.UI_pad_5XL
		uivlist $.UI_pad_3XL [
			if (m_teammode $getmode) [
				uigroup [
					uifont "wide_outline" [
						uioffset (!f $.UI_pad_2XL) (!f $.UI_pad_2XL) [ uitext "V" 1 ]
						uioffset     $.UI_pad_2XL      $.UI_pad_2XL  [ uitext "S" 1 ]
					]
					uihlist $.UI_pad_DS [
						scoreboardteamcard  1 1 0x203070 0x284090
						scoreboardteamcard  2 1 0x702020 0x902828
					]
				]
				scoreboardteamcard -1 3 0x303840 0x404860 $.UI_pad_L
			] [
				scoreboardteamcard  0 2 0x303030 0x404040 $.UI_pad_3XL
				scoreboardteamcard -1 3 0x303840 0x404860 $.UI_pad_L
			]
		]
	]
	uigroup [
		uialign 0 1
		uifont "wide_outline" [ uitext (? $scoreboardmap $scoreboardmap) 1 ]
	]
] [ UIsetbgblur 1 ] [ UIsetbgblur -1 ]
