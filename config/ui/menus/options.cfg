
UImenu "options" [
	uihlist 0 [
		uivlist 0 [
			uifill $.UI_pad_D2XL
			looplist3 i tab icon [ 0 "General" config 1 "Audio" sound 2 "Display" display 3 "Graphics" graphics ] [
				UItabaction uipress+ uihover+ [
					uihover+ [ if (< $i $.UI_curtab) [ .UI_curtab = $i ] ]
				]
				if (= $.UI_curtab $i) [
					UIbuttonarea "hold5" 0 0 [
						uispace 0 $.UI_pad_3XL [
							uivlist $.UI_pad_XL [
								UIfastimg "ui/" $icon 1 $.UI_pad_SXL
								uitext $tab 0.7
							]
						]
					]
				] [
					UIbuttonarea "hold6" 0 0 [
						uispace 0 $.UI_pad_3XL [ uitext $tab 0.7 ]
						UItabaction uipress uihover [
							uihover [ if (> $i $.UI_curtab) [ .UI_curtab = $i ] ]
						]
					]
				]
				uiclamp*x
			]
		]
		UItooltipbox $.UI_pad_UM $.UI_pad_USS [
			uivlist 0 [
				uialign -1 -1
				uifill 0 $.UI_pad_L
				case $.UI_curtab 0 [													// GENERAL TAB
					UIoptrow 0 [ uitext "Aim FOV" 0.6 ] 0 [
						UIhslider fov 50 150 1
					]
					UIoptrow 0 [ uitext "Zoom FOV" 0.6 ] 0 [
						UIhslider zoomfov 10 90 1
					]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 0 [ uitext "Aim Sensitivity" 0.6 ] 0 [
						UIhslider sensitivity 5 40 0.1
					]
					UIoptrow 0 [ uitext "Zoom Sensitivity" 0.6 ] 0 [
						UIhslider zoomsens 1 20 0.1
					]
					UIoptrow 0 [ uitext "UI Sensitivity" 0.6 ] 0 [
						UIhslider uisensitivity 0.1 4 0.1
					]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $.UI_sounds
							uitext "UI Sound Effects" 0.6
						] 0 0 [ .UI_sounds (! $.UI_sounds) ] -1
					]
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $.UI_useimg
							uitext "UI Image Replacements" 0.6
						] 0 0 [ .UI_useimg (! $.UI_useimg) ] -1
					]
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $.UI_clicktab
							uitext "UI Clickable Tabs" 0.6
						] 0 0 [ .UI_clicktab (! $.UI_clicktab) ] -1
					]
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $.UI_blurring
							uitext "UI Background Blur" 0.6
						] 0 0 [
							.UI_blurring (! $.UI_blurring)
							if $.UI_blurring [] [ clearpostfx ]
						] -1
					]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $zoomtoggle
							uitext "Zoom Toggle" 0.6
						] 0 0 [ zoomtoggle (! $zoomtoggle) ] -1
					]
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $invmouse
							uitext "Invert Mouse" 0.6
						] 0 0 [ invmouse (! $invmouse) ] -1
					]
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $showfps
							uitext "Show FPS" 0.6
						] 0 0 [ showfps (! $showfps) ] -1
					]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $wallclock
							uitext "Show Clock" 0.6
						] 0 0 [ wallclock (! $wallclock) ] -1
					]
					UIoptrow 0 [ uicolortext "Format" (? $arg5 $c_gray $c_white) 0.6 ] 2 [
						UIbutton "hold2" [
							UIcheckbox $wallclock24
							uicolortext "24 hours" (? $arg5 $c_gray $c_white) 0.6
						] $.UI_pad_D3XL 0 [ wallclock24 (! $wallclock24) ] -1
						UIbutton "hold2" [
							UIcheckbox $wallclocksecs
							uicolortext "Seconds" (? $arg5 $c_gray $c_white) 0.6
						] $.UI_pad_D3XL 0 [ wallclocksecs (! $wallclocksecs) ] -1
					] [! $wallclock]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 0 [ uitext "Blood Effects" 0.6 ] 2 [
						UIbutton "hold2" [
						    UIcheckbox $gore
						    uitext "Giblets" 0.6
						] $.UI_pad_D3XL 0 [ gore (! $gore) ] -1
						UIbutton "hold2" [
							UIcheckbox $blood
							uitext "Splatter" 0.6
						] $.UI_pad_D3XL 0 [ blood (! $blood) ] -1
					]
					UIbar 0 0 0 $.UI_pad_M
					UIoptrow 1 [
					    UIbutton "hold2" [
					        UIcheckbox $hudgun
					        uitext "Show Weapon" 0.6
					    ] 0 0 [ hudgun (! $hudgun) ] -1
					] 2 [
					    UIbutton "hold2" [
					        UIcheckbox $muzzleflash
					        uitext "Muzzle Flash" 0.6
					    ] $.UI_pad_D3XL 0 [ muzzleflash (! $muzzleflash) ] -1
					] [! $hudgun]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $ragdoll
							uitext "Ragdolls" 0.6
						] 0 0 [ ragdoll (! $ragdoll) ] -1
					]
					UIoptrow 0 [ uicolortext "Fade Time"(? $arg5 $c_gray $c_white) 0.6 ] 0 [
						UIhslider ragdollmillis 0 300000 5000 [] [concat (div $$arg1 1000) "seconds"]
					] [! $ragdoll]
					UIoptrow 0 [ uicolortext "Velocity"(? $arg5 $c_gray $c_white) 0.6 ] 0 [
						UIhslider deadpush 1 20 1 [] [concatword "x" $$arg1]
					] [! $ragdoll]
					UIoptrow 0 [ uicolortext "Gravity"(? $arg5 $c_gray $c_white) 0.6 ] 0 [
						UIhslider ragdollgravity 0 200 2 [] [concatword "x" (+ $$arg1 0)]
					] [! $ragdoll]
					UIbar 0 0 0 $.UI_pad_M
					UIoptrow 0 [ uitext "Player Brightness" 0.6 ] 0 [
						UIhslider fullbrightmodels 0 200 1 [] [concatword $$arg1 "^%"]
					]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 0 [ uitext "Radar" 0.6 ] 2 [
						//UIbutton "hold2" [
						//    UIcheckbox $minimapcompass
						//    uitext "Compass" 0.6
						//] $.UI_pad_D3XL 0 [ minimapcompass (! $minimapcompass) ] -1
						UIbutton "hold2" [
							UIcheckbox $showminimap
							uitext "Minimap" 0.6
						] $.UI_pad_D3XL 0 [ showminimap (! $showminimap) ] -1
					]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 0 [ uitext "Scoreboard" 0.6 ] 2 [
						UIbutton "hold2" [
							UIcheckbox $showkills
							uitext "Kills" 0.6
						] $.UI_pad_D3XL 0 [ showkills (! $showkills) ] -1
						UIbutton "hold2" [
							UIcheckbox $showdeaths
							uitext "Deaths" 0.6
						] $.UI_pad_D3XL 0 [ showdeaths (! $showdeaths) ] -1
						UIbutton "hold2" [
							UIcheckbox $showpj
							uitext "Packet Jump" 0.6
						] $.UI_pad_D3XL 0 [ showpj (! $showpj) ] -1
						UIbutton "hold2" [
							UIcheckbox $showping
							uitext "Ping" 0.6
						] $.UI_pad_D3XL 0 [ showping (! $showping) ] -1
						UIbutton "hold2" [
							UIcheckbox $showclientnum
							uitext "Client#" 0.6
						] $.UI_pad_D3XL 0 [ showclientnum (! $showclientnum) ] -1
						UIbutton "hold2" [
							UIcheckbox $showip
							uitext "IP:Port" 0.6
						] $.UI_pad_D3XL 0 [ showip (! $showip) ] -1
					]
				] 1 [																	// AUDIO TAB
					//UIoptrow 0 [ uitext "Master Volume" 0.6 ] 1 [
					//    UIhbarslider mastervol 0 255 15 0 0 [ .mastervol = $mastervol ]
					//]
					UIoptrow 0 [ uitext "Game Volume" 0.6 ] 1 [
						UIhbarslider soundvol 0 255 15 [ .soundvol = $soundvol ] $.UI_pad_D6XL
					]
					UIoptrow 0 [ uitext "Music Volume" 0.6 ] 1 [
						UIhbarslider musicvol 0 255 15 [ .musicvol = $musicvol ] $.UI_pad_D6XL
					]
					UIoptrow 0 [ uitext "Announcer Volume" 0.6 ] 1 [
						UIhbarslider announcervol 0 255 15 [ .announcervol = $announcervol ] $.UI_pad_D6XL
					]
					UIoptrow 0 [ uitext "UI Sound Volume" 0.6 ] 1 [
						UIhbarslider uivol 0 255 15 [ .uivol = $uivol ] $.UI_pad_D6XL
					]
					//UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					//UIoptrow 0 [ uitext "Mapsound Scale" 0.6 ] 0 [
					//    UIhslider soundenvscale 0 2 0.1 [ .soundenvscale = $soundenvscale ] [ concatword (abs (*f $$arg1 100)) "^%" ]
					//]
					//UIoptrow 0 [ uitext "Events Scale" 0.6 ] 0 [
					//    UIhslider soundevtscale 0 2 0.1 [ .soundevtscale = $soundevtscale ] [ concatword (abs (*f $$arg1 100)) "^%" ]
					//]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 0 [ uitext "Effect Sounds" 0.6 ] 2 [
						UIbutton "hold2" [
					        UIcheckbox $chatsound
					        uitext "Chat Messages" 0.6
					    ] $.UI_pad_D3XL 0 [ chatsound (! $chatsound) ] -1
					    UIbutton "hold2" [
					        UIcheckbox $footstepssound
					        uitext "Footsteps" 0.6
					    ] $.UI_pad_D3XL 0 [ footstepssound (! $footstepssound) ] -1
					    UIbutton "hold2" [
					        UIcheckbox $regensound
					        uitext "Regeneration" 0.6
					    ] $.UI_pad_D3XL 0 [ regensound (! $regensound) ] -1
					    UIbutton "hold2" [
					        UIcheckbox $ambientsounds
					        uitext "Ambients" 0.6
					    ] $.UI_pad_D3XL 0 [ ambientsounds (! $ambientsounds) ] -1
					    //UIbutton "hold2" [
					    //    UIcheckbox $announcersounds
					    //    uitext "Announcer" 0.6
					    //] $.UI_pad_D3XL 0 [ announcersounds (! $announcersounds) ] -1
					    //UIbutton "hold2" [
					    //    UIcheckbox $playmapmusic
					    //    uitext "Map Music" 0.6
					    //] $.UI_pad_D3XL 0 [ playmapmusic (! $playmapmusic) ] -1
					] 0 "Toggle sound effects for various situations"
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 0 [ uitext "Hit Confirmation" 0.6 ] 0 [
						UIoptionslider hitsound [
							0  "DISABLED"     $c_gray_l
							1  "DEFAULT"      $c_white
							2  "ALTERNATIVE"  $c_white
						]
					] 0 "Play a sound to confirm successful hits"
					UIoptrow 0 [ uitext "Kill Confirmation" 0.6 ] 0 [
						UIoptionslider killsound [
							0  "DISABLED"     $c_gray_l
							1  "DEFAULT"      $c_white
							2  "ALTERNATIVE"  $c_white
						]
					] 0 "Play a sound to confirm successful kills"
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 1 [ // bad idea to allow easy toggle on this
						UIbutton "hold2" [
							UIcheckbox $.om_advaudio
							uitext "Access Advanced Settings" 0.6
						] 0 0 [ .om_advaudio (! $.om_advaudio) ] -1
					] 0 [
						if $.om_advaudio [
							UIbutton "hold2" [
								UIcheckbox $sound
								uitext "Allow Game Audio" 0.6
							] 0 0 [ sound (! $sound) ] -1
						]
					]
					if $.om_advaudio [
						UIbar 0 0 0 $.UI_pad_M
						UIoptrow 0 [ uitext "Sound Channels" 0.6 ] 0 [
							UIhslider soundchans 1 128 1
						]
						UIoptrow 0 [ uitext "Sound Buffer Length" 0.6 ] 0 [
							UIhslider soundbufferlen 128 4096 128
						]
						UIbar 0 0 0 $.UI_pad_M
						UIoptrow 0 [ uitext "Sound Frequency" 0.6 ] 1 [
							UIoptionslider soundfreq [
								11025 "11025 Hz" $c_white
								22050 "22050 Hz" $c_white
								44100 "44100 Hz" $c_white
								48000 "48000 Hz" $c_white
							]
						]
						UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
						UIoptrow 1 [
							UIbutton "hold2" [
								UIcheckbox $minimizedsounds
								uitext "Sound When Minimized" 0.6
							] 0 0 [ minimizedsounds (! $minimizedsounds) ] -1
						] 0 "Allow playing sounds from the game when the window is lowered"
						UIoptrow 1 [
							UIbutton "hold2" [
								UIcheckbox $mumble
								uitext "Mumble: Positional Audio" 0.6
							] 0 0 [ mumble (! $mumble) ] -1
						] 0 "Adjust volume and direction of voices based on player position"
					]
				] 2 [																	// DISPLAY TAB
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $fullscreen
							uitext "Exclusive Fullscreen" 0.6
						] 0 0 [
							fullscreen (! $fullscreen)
							if $fullscreen [
								screenw $desktopw
								screenh $desktoph
							]
						] -1
					]
					UIbar 0 0 0 $.UI_pad_M
					if $fullscreen [
						UIoptrow 0 [ uitext "Render Width" 0.6 ] 0 [
							UIhslider screenw (* $.UI_aspectX 60) $desktopw (* $.UI_aspectX 4)
						] 0 "Adjust horizontal render resolution (UI independent)"
						UIoptrow 0 [ uitext "Render Height" 0.6 ] 0 [
							UIhslider screenh (* $.UI_aspectY 60) $desktoph (* $.UI_aspectY 4)
						] 0 "Adjust vertical render resolution (UI independent)"
					] [
						UIoptrow 0 [ uitext "Window Width" 0.6 ] 0 [
							UIhslider screenw 1024 $desktopw 8
						] 0 "Adjust horizontal window size"
						UIoptrow 0 [ uitext "Window Height" 0.6 ] 0 [
							UIhslider screenh 720 $desktoph 8
						] 0 "Adjust vertical window size"
					]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $vsync
							uitext "Vertical Sync" 0.6
						] 0 0 [ vsync (! $vsync) ] -1
					] 0 [
						UIbutton "hold2" [
							UIcheckbox $vsynctear
							uitext "Alternative Tearing Handling" 0.6
						] 0 0 [ vsynctear (! $vsynctear) ] -1
					] 0 "Sync frames to monitor refresh rate"
					UIoptrow 0 [ uitext "Framerate Cap" 0.6 ] 0 [
						UIhslider maxfps 24 480 12
					] $vsync "Upper framerate cap"
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 0 [ uitext "Gamma Correction" 0.6 ] 0 [
						UIhslider gamma 30 300 1 [] [concatword $$arg1 "^%"]
					] 0 "Adjust the luminance value of the display"
				] 3 [																	// GRAPHICS TAB
					UIoptrow 0 [ uitext "Shadow Resolution" 0.6 ] 0 [
						UIoptionslider smsize [
							10 "LOWEST" $c_blue
							11 "LOW"    $c_green
							12 "MEDIUM" $c_yellow
							13 "HIGH"   $c_orange
							14 "ULTRA"  $c_red
						]
					]
					UIoptrow 0 [ uitext "Shadow Filtering" 0.6 ] 0 [
						UIoptionslider smfilter [
							0 "LOW"    $c_blue
							1 "MEDIUM" $c_green
							2 "HIGH"   $c_yellow
							3 "ULTRA"  $c_red
						]
					]
					UIoptrow 0 [ uitext "Anisotropic Filtering" 0.6 ] 0 [
						UIoptionslider aniso [
							0  "OFF" $c_gray_l
							2  "2x"  $c_blue
							4  "4x"  $c_blue
							8  "8x"  $c_blue
							16 "16x" $c_blue
						]
					]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $gi
							uitext "Global Illumination" 0.6
						] 0 0 [ gi (! $gi) ] -1
					] 0 [
						UIoptionslider rhtaps [
							12 "LOW"    $c_blue
							20 "MEDIUM" $c_green
							32 "HIGH"   $c_yellow
						]
					]
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $ao
							uitext "SSAO" 0.6
						] 0 0 [ ao (! $ao) ] -1
					] 0 [
						UIoptionslider aotaps [
							5  "LOW"    $c_blue
							8  "MEDIUM" $c_green
							12 "HIGH"   $c_yellow
						] $ao [ aobilateral (? (>= $aotaps 8) 4 3) ]
					]
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $volumetric
							uitext "Volumetric Lighting" 0.6
						] 0 0 [ volumetric (! $volumetric) ] -1
					] 0 [
						UIoptionslider volsteps [
							12 "LOW"    $c_blue
							16 "MEDIUM" $c_green
							24 "HIGH"   $c_yellow
							48 "ULTRA"  $c_orange
						] $volumetric [ volbilateral (? (>= $volsteps 16) 2 1) ]
					]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $stains
							uitext "Stains" 0.6
						] 0 0 [ stains (! $stains) ] -1
					]
					UIoptrow 0 [ uicolortext "Fade Distance" (? $arg5 $c_gray $c_white) 0.6 ] 0 [
						UIhslider maxstaindistance 256 8192 256 [] [concat $$arg1 "units"]
					] [! $stains]
					UIoptrow 0 [ uicolortext "Fade Time" (? $arg5 $c_gray $c_white) 0.6 ] 0 [
						UIhslider stainfade 2 60 1 [ maxstaintris (? (> $stainfade 15) 8192 2048) ] [concat $$arg1 "seconds"]
					] [! $stains]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $grass
							uitext "3D Grass" 0.6
						] 0 0 [ grass (! $grass) ] -1
					]
					UIoptrow 0 [ uicolortext "Fade Distance" (? $arg5 $c_gray $c_white) 0.6 ] 0 [
						UIhslider grassdist 128 1024 32 [] [concat $$arg1 "units"]
					] [! $grass]
					UIoptrow 0 [ uicolortext "Blade Sparsity" (? $arg5 $c_gray $c_white) 0.6 ] 0 [
						UIhslider grassstep 0.5 8.0 0.1 [] [concat $$arg1 "units"]
					] [! $grass]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 0 [ uitext "Water Quality" 0.6 ] 2 [
						UIbutton "hold2" [
							UIcheckbox $waterreflect
							uitext "Reflection" 0.6
						] $.UI_pad_D3XL 0 [ waterreflect (! $waterreflect) ] -1
						UIbutton "hold2" [
							UIcheckbox $vertwater
							uitext "Animation" 0.6
						] $.UI_pad_D3XL 0 [ vertwater (! $vertwater) ] -1
					]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 0 [ uitext "Soft Blending" 0.6 ] 2 [
						UIbutton "hold2" [
							UIcheckbox $softparticles
							uitext "Particles" 0.6
						] $.UI_pad_D3XL 0 [ softparticles (! $softparticles) ] -1
						UIbutton "hold2" [
							UIcheckbox $softexplosion
							uitext "Explosions" 0.6
						] $.UI_pad_D3XL 0 [ softexplosion (! $softexplosion) ] -1
						//UIbutton "hold2" [
						//	UIcheckbox $growingparticles
						//	uitext "Growing Prtcls" 0.6
						//] $.UI_pad_D3XL 0 [ growingparticles (! $growingparticles) ] -1
					]
					UIbar 0 0 0 $.UI_pad_M
					UIoptrow 0 [ uicolortext "Particle Quality" (? $arg5 $c_gray $c_white) 0.6 ] 0 [
						UIhslider softparticleblend 1 64 1 [] [concat $$arg1 "passes"]
					] [! $softparticles]
					UIoptrow 0 [ uicolortext "Explosion Quality" (? $arg5 $c_gray $c_white) 0.6 ] 0 [
						UIhslider softexplosionblend 1 64 1 [] [concat $$arg1 "passes"]
					] [! $softexplosion]
					UIbar 1 0 $.UI_pad_6XL $.UI_pad_2XL
					UIoptrow 0 [ uitext "Multisample AA" 0.6 ] 0 [
						UIoptionslider msaa [
							0  "OFF" $c_gray_l
							2  "2x"  $c_blue
							4  "4x"  $c_yellow
							8  "8x"  $c_orange
							16 "16x" $c_red
						]
					]
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $fxaa
							uitext "FXAA" 0.6
						] 0 0 [ fxaa (! $fxaa) ] -1
					] 0 [
						UIoptionslider fxaaquality [
							0 "LOW"    $c_blue
							1 "MEDIUM" $c_blue
							2 "HIGH"   $c_green
							3 "ULTRA"  $c_green
						] $fxaa
					]
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $smaa
							uitext "Morphological AA" 0.6
						] 0 0 [ smaa (! $smaa) ] -1
					] 0 [
						UIoptionslider smaaquality [
							0 "LOW"    $c_blue
							1 "MEDIUM" $c_green
							2 "HIGH"   $c_yellow
							3 "ULTRA"  $c_orange
						] $smaa [ smaacoloredge (>= $smaaquality 3) ]
					]
					UIoptrow 1 [
						UIbutton "hold2" [
							UIcheckbox $tqaa
							uitext "Temporal AA" 0.6
						] 0 0 [ tqaa (! $tqaa) ] -1
					]
				]
				uifill 0 $.UI_pad_L
				uialign* -1
			]
		] 0 [
			if $pendingchanges [
				uiline $c_line1 $.UI_pad_S
				uiclamp-y
				uitextfill 8.5 0.5 [
					uitext (format "^fs^f0%1 ^fSchange%2 pending" $pendingchanges (? (> $pendingchanges 1) "s" " ")) 0.5
					uialign- 1
				]
			]
		]
	]
] [
	.UI_curtab = 0
	local gcdratio
	gcdratio = (gcd $desktopw $desktoph)
	.UI_aspectX = (div $desktopw $gcdratio)
	.UI_aspectY = (div $desktoph $gcdratio)
] [ if $pendingchanges [ showui "dialog_changes" ] ] [] [ uitext "Options" 0.8 ]
