
UImenu "main" [
	uieschide (! $mainmenu)
	uihlist 0 [
		uivlist 0 [
			uifill $.UI_pad_D3XL
			UIbutton "hold2" [ uitext "Server Browser" 0.6 ] 0 $.UI_pad_6XL [ changeui "servers" ]
			UIbutton "hold2" [ uitext "Map Browser"    0.6 ] 0 $.UI_pad_6XL [ changeui "map_browser" ]
			UIbar 1 0 0 $.UI_pad_L
			UIbutton "hold2" [ uitext "Player Profile" 0.6 ] 0 $.UI_pad_6XL [ changeui "profile" ]
			UIbutton "hold2" [ uitext "Options"        0.6 ] 0 $.UI_pad_6XL [ changeui "options" ]
			UIbutton "hold2" [ uitext "Controls"       0.6 ] 0 $.UI_pad_6XL [ changeui "keybinds" ]
			UIbutton "hold2" [ uitext "Recorder"       0.6 ] 0 $.UI_pad_6XL [ changeui "recorder" ]
			if $mainmenu [
				UIbutton "hold2" [ uitext "Credits"    0.6 ] 0 $.UI_pad_6XL [ changeui "credits" ]
			]
			UIbar 1 0 0 $.UI_pad_L
			UIbutton "hold2" [ uitext "Quit"           0.6 ] 0 $.UI_pad_6XL [
				if $confirmquit [ changeui "dialog_quit" ] quit
			]
			uiclamp*x
		]
		if $mainmenu [
			// blank
			// UIbar 0 0 $.UI_pad_L
			// UIclock 0.36 0 3
		] [
			UIbar 0 1 $.UI_pad_L
			uivlist 0 [
				uifill $.UI_pad_D3XL
				if (isspectator $getclientnum) [
					local canplay
					canplay = (|| [isconnected 0 0] [< $getmastermode 2] [ismaster $getclientnum])
					UIbutton "hold2" [
						uitext (concatword (? $canplay "" "^f4") "Join Game") 0.6
					] 0 $.UI_pad_6XL [ if $canplay [ hideui $uiname ; spectator 0 ] ]
				] [ UIbutton "hold2" [ uitext "Spectate"     0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; spectator 1 ] ]
				if (getmode $m_default) [
					UIbutton "hold2" [ uitext "Change Loadout" 0.6 ] 0 $.UI_pad_6XL [ changeui "loadout" ]
				]
				case $getteam 0 [ uifill 0 $.UI_pad_6XL ] 1 [
					UIbutton "hold2" [ uitext "Join ^f3Vanir" 0.6 ] 0 $.UI_pad_6XL [ team "Vanir" ]
				] 2 [
					UIbutton "hold2" [ uitext "Join ^f1Aesir" 0.6 ] 0 $.UI_pad_6XL [ team "Aesir" ]
				]
				//uifill 0 $.UI_pad_6XL
				UIbar 1 0 0 $.UI_pad_L
				UIbutton "hold2" [
					uivlist $.UI_pad_L [
						uitext "Client Control" 0.6
						uitext "Interface" 0.6
					]
				] 0 (*f $.UI_pad_6XL 2) [ changeui "cci" ]
				UIbar 1 0 0 $.UI_pad_L
				UIbutton "hold2" [ uitext "Disconnect" 0.6 ] 0 $.UI_pad_6XL [ disconnect ]
				uiclamp*x
			]
		]
	]
] [
	if (&& [! $.firstrun] [=s $getname "player"]) [
		changeui "welcome" "main"
	] [
		if $mainmenu [ normkey "ESCAPE" 0 ]
		normkey "TAB" [ changeui "servers" "main" ]
	]
] [
	normkey "ESCAPE"
	normkey "TAB"
]
